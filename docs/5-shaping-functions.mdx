---
id: 5-shaping-functions
title: 5 - Shaping Functions
---

import ResponsiveShader from "@theme/ResponsiveShader";

## Chapter 5 - Shaping Functions

In [chapter 5 of the Book of Shaders](https://thebookofshaders.com/05/) introduces us to the essential [kata](https://en.wikipedia.org/wiki/Kata) of shaders, shaping functions. GLSL includes a number of shaping functions like [smoothstep](https://thebookofshaders.com/glossary/?search=smoothstep). We can also build our own custom ones using built in math functions like sin.

Like with the other chapters my shader explorations led to a number of rabbit holes, including [simple bit-mapped text output](https://www.shadertoy.com/view/4dtGD2) and [2D signed-distance functions](https://www.iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm). The end result was a gallery of shaping functions.

## Smooth Lines and Sinusoids

The first shader I made for this chapter was an animated sine wave. The background of the shader is a gradient produced by the sin function, with the actual sin function plotted over top. The plotting is anti-aliased using a neat trick shown in this chapter that uses use two `smoothstep` to produce a "rounded" line:

```glsl
// Plot a line on Y using a value between 0.0-1.0
// st = texture coordinates   pct = % from 0.0 to 1.0
float plotIt(vec2 st, float pct){
  const float halfSmoothWidth = 0.006;
  return  smoothstep(pct - halfSmoothWidth, pct, st.y)
          - smoothstep(pct, pct + halfSmoothWidth, st.y);
}
```

### Animated Sine Wave - Shader

<ResponsiveShader
  maxWidth="350"
  height="350"
  fragShaderPath="/shaders/5.0-animated-sine-wave.frag"
/>

### Animated Sine Wave - GLSL Source

The following GLSL code assumes a WebGL platform.

```glsl
#ifdef GL_ES
precision mediump float;
#endif

#define PI 3.14159265358979

uniform vec2 u_resolution;
uniform float u_time;

// Plot a line on Y using a value between 0.0-1.0
// st = texture coordinates   pct = % from 0.0 to 1.0
float plotIt(vec2 st, float pct){
  const float halfSmoothWidth = 0.006;
  return  smoothstep(pct - halfSmoothWidth, pct, st.y)
          - smoothstep(pct, pct + halfSmoothWidth, st.y);
}

void main() {
  const vec3 plotColour = vec3(.38, .31, .58); // Plot colour.
  vec2 st = gl_FragCoord.xy / u_resolution.xy; // Normalize texture coords from 0.0 to 1.0.

  float y = sin(st.x * PI + u_time); // y is a sinusoid based on x position and time.
        y = 0.5 * y + 0.5;           // Scale and offset y to center plot.

  float plot = plotIt(st, y);          // Create a plot of y.


  float colour  = y;  // Black to white background gradient.
        colour -= plot;   // Remove some of gradient "below" where plot will be drawn.
        colour  = max(colour, 0.0); // Negative values become zero.

  vec3 colours = vec3(colour)
                 + vec3(plot) * plotColour;

  gl_FragColor = vec4(colours, 1.0);  // Set pixel colour.
}

// Stung Eye 2020 - Unlicense - https://unlicense.org
// This is free and unencumbered software released into the public domain.
```

> [Download `5.0-animated-sine-wave.frag`](/shaders/5.0-animated-sine-wave.frag).

## Even Smoother Lines

<ResponsiveShader
  maxWidth="530"
  height="530"
  fragShaderPath="/shaders/5.2-shaping-gallery.frag"
/>

Sinusoidal background gradient with function plotted over top.

This is an animated shader. Hover and press play.
